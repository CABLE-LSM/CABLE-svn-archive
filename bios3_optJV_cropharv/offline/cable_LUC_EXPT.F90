MODULE CABLE_LUC_EXPT

  USE netcdf
  USE CABLE_COMMON_MODULE, ONLY: IS_LEAPYEAR, LEAP_DAY, &
       HANDLE_ERR, GET_UNIT

  USE cable_IO_vars_module, ONLY: logn, land_x, land_y, landpt, latitude, longitude
  USE cable_def_types_mod,  ONLY: mland, r_2

  IMPLICIT NONE

  TYPE LUC_INPUT_TYPE
     REAL, DIMENSION(:), ALLOCATABLE :: VAL
   !  INTEGER :: YEAR
  END TYPE LUC_INPUT_TYPE

  TYPE LUC_EXPT_TYPE
     CHARACTER(len=400)::	TransitionFilePath,ClimateFile, Run, NotPrimOnlyFile 
     LOGICAL  :: DirectRead, READrst, WRITErst
     LOGICAL, ALLOCATABLE ::  prim_only(:)
     LOGICAL, ALLOCATABLE :: ptos(:), ptog(:), stog(:), gtos(:)
     LOGICAL, ALLOCATABLE :: ptoc(:), ptoq(:), stoc(:), stoq(:), ctos(:), qtos(:)
     INTEGER, ALLOCATABLE :: ivegp(:)
     INTEGER, ALLOCATABLE :: biome(:)
     INTEGER :: YearStart, YearEnd, nfile
     INTEGER :: CTSTEP
     REAL, ALLOCATABLE :: primaryf(:), mtemp_min20(:), grass(:), secdf(:), crop(:), past(:)
     CHARACTER(len=200),DIMENSION(17) :: TransFile
     CHARACTER(len=12) ,DIMENSION(17) :: VAR_NAME
     INTEGER,           DIMENSION(17) :: F_ID, V_ID
     TYPE(LUC_INPUT_TYPE), DIMENSION(17):: INPUT
     INTEGER :: YEAR, ydimsize, xdimsize, nrec, FirstYear

  END TYPE LUC_EXPT_TYPE

  TYPE (LUC_EXPT_TYPE), SAVE :: LUC_EXPT

    INTEGER,  PARAMETER :: &
       ptos         =  1, &
       ptog         =  2, &
       stog         =  3, &
       gtos         =  4, &
       grassfrac    =  5, &
       primffrac    =  6, &
       pharv        =  7, &
       smharv       =  8, &
       syharv       =  9, &
       ptoc         =  10, &
       ptoq         =  11, &
       stoc         =  12, &
       stoq         =  13, &
       ctos         =  14, &
       qtos         =  15, &
       cropfrac = 16, &
       pastfrac = 17


CONTAINS

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  ! CABLE_LUC_EXPT routines
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

! ==============================================================================
  SUBROUTINE LUC_EXPT_INIT( LUC_EXPT)

    IMPLICIT NONE
    
    TYPE (LUC_EXPT_TYPE), INTENT(INOUT) :: LUC_EXPT
    
    REAL :: tmp
    REAL,ALLOCATABLE :: tmparr(:,:)  , lattmp(:),lontmp(:)
    INTEGER :: t, i, ii, k, x, y, realk
    INTEGER :: fID, vID, timID,latID, lonID, tdimsize, xdimsize, ydimsize
    INTEGER :: xds, yds, tds
    INTEGER :: STATUS,  iu
    CHARACTER(len=15)    :: Run
    CHARACTER(len=400)   :: TransitionFilePath, ClimateFile, NotPrimOnlyFile
    LOGICAL :: DirectRead
    INTEGER :: YearStart, YearEnd
    REAL, ALLOCATABLE :: tmpvec(:), tmparr3(:,:,:)
    INTEGER :: NotPrimOnly_fID, NotPrimOnly_vID
       
    NAMELIST /LUCNML/  TransitionFilePath, ClimateFile, Run, DirectRead, YearStart, YearEnd, &
         NotPrimOnlyFile
    
    ALLOCATE( LUC_EXPT%prim_only(mland) )
    ALLOCATE( LUC_EXPT%ivegp(mland) )
    ALLOCATE( LUC_EXPT%biome(mland) )
    ALLOCATE( LUC_EXPT%ptos(mland) )
    ALLOCATE( LUC_EXPT%ptog(mland) )
    ALLOCATE( LUC_EXPT%stog(mland) )
    ALLOCATE( LUC_EXPT%gtos(mland) )
    ALLOCATE( LUC_EXPT%ptoc(mland) )
    ALLOCATE( LUC_EXPT%ptoq(mland) )
    ALLOCATE( LUC_EXPT%ctos(mland) )
    ALLOCATE( LUC_EXPT%qtos(mland) )
    ALLOCATE( LUC_EXPT%stoc(mland) )
    ALLOCATE( LUC_EXPT%stoq(mland) )
    ALLOCATE( LUC_EXPT%primaryf(mland) )
    ALLOCATE( LUC_EXPT%secdf(mland) )
    ALLOCATE( LUC_EXPT%grass(mland) )
    ALLOCATE( LUC_EXPT%crop(mland) )
    ALLOCATE( LUC_EXPT%past(mland) )
    ALLOCATE( LUC_EXPT%mtemp_min20(mland) )
    LUC_EXPT%NotPrimOnlyFile = 'none'
    ! READ LUC_EXPT settings
    CALL GET_UNIT(iu)
    OPEN (iu,FILE="LUC.nml",STATUS='OLD',ACTION='READ')
    READ (iu,NML=LUCNML)
    CLOSE(iu)
    LUC_EXPT%TransitionFilePath = TransitionFilePath
    LUC_EXPT%ClimateFile        = ClimateFile
    LUC_EXPT%DirectRead         = DirectRead
    LUC_EXPT%YearStart          = YearStart
    LUC_EXPT%YearEnd            = YearEnd
    LUC_EXPT%NotPrimOnlyFile      = NotPrimOnlyFile

    WRITE(*   ,*)"================== LUC_EXPT  ============"
    WRITE(*   ,*)"LUC_EXPT settings chosen:"
    WRITE(*   ,*)" TransitionFilePath: ",TRIM(LUC_EXPT%TransitionFilePath)
    WRITE(*   ,*)" ClimateFile       : ",TRIM(LUC_EXPT%ClimateFile)


    ! Transition Filenames and variables
    LUC_EXPT%TransFile(1) = TRIM(LUC_EXPT%TransitionFilePath)//"/ptos.nc"
    LUC_EXPT%TransFile(2) = TRIM(LUC_EXPT%TransitionFilePath)//"/ptog.nc"
    LUC_EXPT%TransFile(3) = TRIM(LUC_EXPT%TransitionFilePath)//"/stog.nc"
    LUC_EXPT%TransFile(4) = TRIM(LUC_EXPT%TransitionFilePath)//"/gtos.nc"
    LUC_EXPT%TransFile(5) = TRIM(LUC_EXPT%TransitionFilePath)//"/grass.nc"
    LUC_EXPT%TransFile(6) = TRIM(LUC_EXPT%TransitionFilePath)//"/primaryf.nc"
    LUC_EXPT%TransFile(7) = TRIM(LUC_EXPT%TransitionFilePath)//"/pharv.nc"
    LUC_EXPT%TransFile(8) = TRIM(LUC_EXPT%TransitionFilePath)//"/smharv.nc"
    LUC_EXPT%TransFile(9) = TRIM(LUC_EXPT%TransitionFilePath)//"/syharv.nc"

    LUC_EXPT%TransFile(10) = TRIM(LUC_EXPT%TransitionFilePath)//"/ptoc.nc"
    LUC_EXPT%TransFile(11) = TRIM(LUC_EXPT%TransitionFilePath)//"/ptoq.nc"
    LUC_EXPT%TransFile(12) = TRIM(LUC_EXPT%TransitionFilePath)//"/stoc.nc"
    LUC_EXPT%TransFile(13) = TRIM(LUC_EXPT%TransitionFilePath)//"/stoq.nc"
    LUC_EXPT%TransFile(14) = TRIM(LUC_EXPT%TransitionFilePath)//"/ctos.nc"
    LUC_EXPT%TransFile(15) = TRIM(LUC_EXPT%TransitionFilePath)//"/qtos.nc"
    LUC_EXPT%TransFile(16) = TRIM(LUC_EXPT%TransitionFilePath)//"/crop.nc"
    LUC_EXPT%TransFile(17) = TRIM(LUC_EXPT%TransitionFilePath)//"/past.nc"


    LUC_EXPT%VAR_NAME(1) = 'ptos'
    LUC_EXPT%VAR_NAME(2) = 'ptog'
    LUC_EXPT%VAR_NAME(3) = 'stog'
    LUC_EXPT%VAR_NAME(4) = 'gtos'
    LUC_EXPT%VAR_NAME(5) = 'grass'
    LUC_EXPT%VAR_NAME(6) = 'primaryf'
    LUC_EXPT%VAR_NAME(7) = 'pharv'
    LUC_EXPT%VAR_NAME(8) = 'smharv'
    LUC_EXPT%VAR_NAME(9) = 'syharv'
    
    LUC_EXPT%VAR_NAME(10) = 'ptoc'
    LUC_EXPT%VAR_NAME(11) = 'ptoq'
    LUC_EXPT%VAR_NAME(12) = 'stoc'
    LUC_EXPT%VAR_NAME(13) = 'stoq'
    LUC_EXPT%VAR_NAME(14) = 'ctos'
    LUC_EXPT%VAR_NAME(15) = 'qtos'
    LUC_EXPT%VAR_NAME(16) = 'crop'
    LUC_EXPT%VAR_NAME(17) = 'past'


    LUC_EXPT%nfile = 17

    DO x = 1, LUC_EXPT%nfile
       ALLOCATE( LUC_EXPT%INPUT(x)%VAL(mland) )
    END DO

! OPEN LUC INPUT FILES
    DO i = 1, LUC_EXPT%nfile

       WRITE(*   ,*) 'LUC input data file: ', LUC_EXPT%TransFile(i)
       WRITE(logn,*) 'LUC input data file: ', LUC_EXPT%TransFile(i)
      
       STATUS = NF90_OPEN(TRIM(LUC_EXPT%TransFile(i)), NF90_NOWRITE, LUC_EXPT%F_ID(i))
       CALL HANDLE_ERR(STATUS, "Opening LUH2 file "//LUC_EXPT%TransFile(i) )
       STATUS = NF90_INQ_VARID(LUC_EXPT%F_ID(i),TRIM(LUC_EXPT%VAR_NAME(i)), LUC_EXPT%V_ID(i))
       CALL HANDLE_ERR(STATUS, "Inquiring LUC_EXPT var "//TRIM(LUC_EXPT%VAR_NAME(i))// &
            " in "//LUC_EXPT%TransFile(i) )

       ! inquire dimensions
       IF (i.eq.1) THEN
          FID = LUC_EXPT%F_ID(i)
          STATUS = NF90_INQ_DIMID(FID,'lat',latID)
          STATUS = NF90_INQUIRE_DIMENSION(FID,latID,len=ydimsize)
          CALL HANDLE_ERR(STATUS, "Inquiring 'lat'"//TRIM(LUC_EXPT%TransFile(i)))
          LUC_EXPT%ydimsize = ydimsize

          STATUS = NF90_INQ_DIMID(FID,'lon',lonID)
          STATUS = NF90_INQUIRE_DIMENSION(FID,lonID,len=xdimsize)
          CALL HANDLE_ERR(STATUS, "Inquiring 'lon'"//TRIM(LUC_EXPT%TransFile(i)))
          LUC_EXPT%xdimsize = xdimsize

          STATUS = NF90_INQ_DIMID(FID,'time',timID)
          STATUS = NF90_INQUIRE_DIMENSION(FID,timID,len=tdimsize)
          CALL HANDLE_ERR(STATUS, "Inquiring 'time'"//TRIM(LUC_EXPT%TransFile(i)))
          LUC_EXPT%nrec = tdimsize

!!$          STATUS = NF90_GET_VAR( Luc_expt%f_id(i), timID, tmp, &
!!$               start=(/1,1,1/) )
!!$          CALL HANDLE_ERR(STATUS, "Reading from "//LUC_EXPT%TransFile(i) )


          xds = LUC_EXPT%xdimsize
          yds = LUC_EXPT%ydimsize
       ENDIF
     !write(*,*) 'length LUH2 data: ', tdimsize
    ENDDO

    LUC_EXPT%FirstYEAR = 850
  ! Set internal counter
    LUC_EXPT%CTSTEP = 1 +  LUC_EXPT%YearStart- LUC_EXPT%FirstYEAR

    allocate(tmparr(xds,yds))

 ! READ initial states
    i = grassfrac
    IF ( LUC_EXPT%DirectRead ) THEN

       DO k = 1, mland
          STATUS = NF90_GET_VAR( LUC_EXPT%F_ID(i), LUC_EXPT%V_ID(i), tmp, &
               start=(/land_x(k),land_y(k),LUC_EXPT%CTSTEP/) )
          CALL HANDLE_ERR(STATUS, "Reading direct from "//LUC_EXPT%TransFile(i) )
          LUC_EXPT%grass(k) = tmp
       END DO
    ELSE
       STATUS = NF90_GET_VAR(LUC_EXPT%F_ID(i), LUC_EXPT%V_ID(i), tmparr, &
            start=(/1,1,LUC_EXPT%CTSTEP/),count=(/xds,yds,1/) )
       CALL HANDLE_ERR(STATUS, "Reading from "//LUC_EXPT%TransFile(i) )
       
       DO k = 1, mland
          LUC_EXPT%grass(k) = tmparr( land_x(k), land_y(k) )
       END DO
    ENDIF

    i = primffrac
    IF ( LUC_EXPT%DirectRead ) THEN
       DO k = 1, mland
           STATUS = NF90_GET_VAR( LUC_EXPT%F_ID(i), LUC_EXPT%V_ID(i), tmp, &
               start=(/land_x(k),land_y(k),LUC_EXPT%CTSTEP/) )
          CALL HANDLE_ERR(STATUS, "Reading direct from "//LUC_EXPT%TransFile(i) )
          LUC_EXPT%primaryf(k) = tmp
       END DO
    ELSE
       STATUS = NF90_GET_VAR(LUC_EXPT%F_ID(i), LUC_EXPT%V_ID(i), tmparr, &
            start=(/1,1,LUC_EXPT%CTSTEP/),count=(/xds,yds,1/) )
       CALL HANDLE_ERR(STATUS, "Reading from "//LUC_EXPT%TransFile(i) )
       
       DO k = 1, mland
          LUC_EXPT%primaryf(k) = tmparr( land_x(k), land_y(k) )
       END DO

    ENDIF

    i = cropfrac
    IF ( LUC_EXPT%DirectRead ) THEN
       DO k = 1, mland
          STATUS = NF90_GET_VAR( LUC_EXPT%F_ID(i), LUC_EXPT%V_ID(i), tmp, &
               start=(/land_x(k),land_y(k),LUC_EXPT%CTSTEP/) )
          CALL HANDLE_ERR(STATUS, "Reading direct from "//LUC_EXPT%TransFile(i) )
          LUC_EXPT%crop(k) = tmp
       END DO
    ELSE
       STATUS = NF90_GET_VAR(LUC_EXPT%F_ID(i), LUC_EXPT%V_ID(i), tmparr, &
            start=(/1,1,LUC_EXPT%CTSTEP/),count=(/xds,yds,1/) )
       CALL HANDLE_ERR(STATUS, "Reading from "//LUC_EXPT%TransFile(i) )
       
       DO k = 1, mland
          LUC_EXPT%crop(k) = tmparr( land_x(k), land_y(k) )
       END DO
       
    ENDIF


    i = pastfrac
    IF ( LUC_EXPT%DirectRead ) THEN
       DO k = 1, mland
          STATUS = NF90_GET_VAR( LUC_EXPT%F_ID(i), LUC_EXPT%V_ID(i), tmp, &
               start=(/land_x(k),land_y(k),LUC_EXPT%CTSTEP/) )
          CALL HANDLE_ERR(STATUS, "Reading direct from "//LUC_EXPT%TransFile(i) )
          LUC_EXPT%past(k) = tmp
       END DO
    ELSE
       STATUS = NF90_GET_VAR(LUC_EXPT%F_ID(i), LUC_EXPT%V_ID(i), tmparr, &
            start=(/1,1,LUC_EXPT%CTSTEP/),count=(/xds,yds,1/) )
       CALL HANDLE_ERR(STATUS, "Reading from "//LUC_EXPT%TransFile(i) )
       
       DO k = 1, mland
          LUC_EXPT%past(k) = tmparr( land_x(k), land_y(k) )
       END DO
       
    ENDIF 

    LUC_EXPT%grass = min(LUC_EXPT%grass, 1.0)
    LUC_EXPT%primaryf = min(LUC_EXPT%primaryf, 1.0- LUC_EXPT%grass)
    LUC_EXPT%secdf = max((1.0 -  LUC_EXPT%grass - LUC_EXPT%primaryf), 0.0)
    LUC_EXPT%crop = max(min( LUC_EXPT%crop, LUC_EXPT%grass),0.0)
    LUC_EXPT%past =max( min(LUC_EXPT%grass - LUC_EXPT%crop, LUC_EXPT%past), 0.0)


    CALL READ_ClimateFile(LUC_EXPT)
    
    ! hot desert
    WHERE (LUC_EXPT%biome.eq.15 )
       LUC_EXPT%ivegp = 14
    ENDWHERE

    WHERE (LUC_EXPT%biome .eq. 3 .or. LUC_EXPT%biome .eq. 11) ! savanna/ xerophytic woods
       LUC_EXPT%grass = LUC_EXPT%grass + (LUC_EXPT%primaryf+LUC_EXPT%secdf)*3.0/5.0
       LUC_EXPT%primaryf =  LUC_EXPT%primaryf * 2.0/5.0
       LUC_EXPT%secdf =  LUC_EXPT%secdf * 2.0/5.0
    ELSEWHERE (LUC_EXPT%biome .eq. 12 .or. LUC_EXPT%biome .eq. 13 & ! shrub
         .or. LUC_EXPT%biome .eq. 15 .or. LUC_EXPT%biome .eq. 16  )
       LUC_EXPT%grass = LUC_EXPT%grass + (LUC_EXPT%primaryf+LUC_EXPT%secdf)*4.0/5.0
       LUC_EXPT%primaryf =  LUC_EXPT%primaryf * 1.0/5.0
       LUC_EXPT%secdf =  LUC_EXPT%secdf * 1.0/5.0
    ELSEWHERE (LUC_EXPT%biome .eq. 7 .or. LUC_EXPT%biome .eq. 8 &  ! boreal
         .or. LUC_EXPT%biome .eq. 9 .or. LUC_EXPT%biome .eq. 10  )
       LUC_EXPT%grass = LUC_EXPT%grass + (LUC_EXPT%primaryf+LUC_EXPT%secdf)*1.0/5.0
       LUC_EXPT%primaryf =  LUC_EXPT%primaryf * 4.0/5.0
       LUC_EXPT%secdf =  LUC_EXPT%secdf * 4.0/5.0
    ELSEWHERE (LUC_EXPT%biome .eq. 5 .or. LUC_EXPT%biome .eq. 6 ) ! DBL
       LUC_EXPT%grass = LUC_EXPT%grass + (LUC_EXPT%primaryf+LUC_EXPT%secdf)*0.3
       LUC_EXPT%primaryf =  LUC_EXPT%primaryf *0.7
       LUC_EXPT%secdf =  LUC_EXPT%secdf * 0.7
    END WHERE

    write(59,*) TRIM(LUC_EXPT%NotPrimOnlyFile), (TRIM(LUC_EXPT%NotPrimOnlyFile).EQ.'none')
    ! READ transitions from primary to see if primary remains primary
    if (TRIM(LUC_EXPT%NotPrimOnlyFile).EQ.'none')   THEN     
       LUC_EXPT%prim_only = .TRUE.
       IF(.NOT.ALLOCATED(tmpvec)) ALLOCATE(tmpvec(tdimsize))
       IF(.NOT.ALLOCATED(tmparr3)) ALLOCATE(tmparr3(xds,yds,tdimsize))
       DO i=1,2 ! ptos and ptog
          IF ( LUC_EXPT%DirectRead ) THEN
             DO k = 1, mland
                
                STATUS = NF90_GET_VAR( LUC_EXPT%F_ID(i), LUC_EXPT%V_ID(i), tmpvec, &
                     start=(/land_x(k),land_y(k),1/), &
                     count=(/1,1,tdimsize/) )
                CALL HANDLE_ERR(STATUS, "Reading direct from "//LUC_EXPT%TransFile(i) )

                !IF (sum(tmpvec).gt.1e-3 .OR. LUC_EXPT%primaryf(k).lt.0.99) LUC_EXPT%prim_only(k) = .FALSE.
                IF (sum(tmpvec).gt.1e-3 ) LUC_EXPT%prim_only(k) = .FALSE.
             END DO
             
          ELSE
             STATUS = NF90_GET_VAR(LUC_EXPT%F_ID(i), LUC_EXPT%V_ID(i), tmparr3, &
                  start=(/1,1,1/),count=(/xds,yds,tdimsize/) )
             CALL HANDLE_ERR(STATUS, "Reading from "//LUC_EXPT%TransFile(i) )
             DO k = 1, mland
                tmpvec = tmparr3(  land_x(k), land_y(k) , :)
                ! IF (sum(tmpvec).gt.1e-3.OR. LUC_EXPT%primaryf(k).lt.0.99) LUC_EXPT%prim_only(k) = .FALSE.
                IF (sum(tmpvec).gt.1e-3) LUC_EXPT%prim_only(k) = .FALSE.
             END DO
             
          ENDIF
          
       END DO
    ELSE
       tmparr = 0.0
       LUC_EXPT%prim_only = .TRUE.
       Status = NF90_OPEN(TRIM(NotPrimOnlyFile), NF90_NOWRITE, NotPrimOnly_fID)
       Status = NF90_INQ_VARID( NotPrimOnly_fID,'cum_frac_prim_loss',  NotPrimOnly_vID)
       STATUS = NF90_GET_VAR(NotPrimOnly_FID, NotPrimOnly_vID , tmparr, &
            start=(/1,1/),count=(/xds,yds/) )
       i = 0
       DO k = 1, mland
          if (tmparr( land_x(k), land_y(k)) .gt. 1e-3) then
             LUC_EXPT%prim_only(k) = .FALSE.
             i=i+1
          endif
       ENDDO
       PRINT *,  "number of not prim_only grid-cells: ", i
       PRINT *,  "number grid-cells: ", mland
       STATUS = NF90_CLOSE(NotPrimOnly_fID)
    ENDIF
    
      


    

    ! set secondary vegetation area to be zero where land use transitions don't occur
    ! set grass component of primary vegetation cover
    WHERE (LUC_EXPT%prim_only .eq. .TRUE.)
       LUC_EXPT%secdf = 0.0
       LUC_EXPT%primaryf = 1.0
       LUC_EXPT%grass = 0.0
       WHERE (LUC_EXPT%biome .eq. 3 .or. LUC_EXPT%biome .eq. 11) ! savanna/ xerophytic woods
          LUC_EXPT%grass = LUC_EXPT%primaryf*3.0/5.0
          LUC_EXPT%primaryf =  LUC_EXPT%primaryf * 2.0/5.0
       ELSEWHERE (LUC_EXPT%biome .eq. 12 .or. LUC_EXPT%biome .eq. 13 &
            .or. LUC_EXPT%biome .eq. 15 .or. LUC_EXPT%biome .eq. 16  ) ! shrub
          LUC_EXPT%grass = LUC_EXPT%primaryf*4.0/5.0
          LUC_EXPT%primaryf =  LUC_EXPT%primaryf * 1.0/5.0
       ELSEWHERE (LUC_EXPT%biome .eq. 7 .or. LUC_EXPT%biome .eq. 8 &
            .or. LUC_EXPT%biome .eq. 9 .or. LUC_EXPT%biome .eq. 10) ! boreal
          LUC_EXPT%grass = LUC_EXPT%primaryf*1.0/5.0
          LUC_EXPT%primaryf =  LUC_EXPT%primaryf * 4.0/5.0
       ELSEWHERE (LUC_EXPT%biome .eq. 5 .or. LUC_EXPT%biome .eq. 6 ) ! DBL
          LUC_EXPT%grass = LUC_EXPT%primaryf*0.3
          LUC_EXPT%primaryf =  LUC_EXPT%primaryf *0.7
       END WHERE
    END WHERE

 END SUBROUTINE LUC_EXPT_INIT
  
! ==============================================================================


 SUBROUTINE LUC_EXPT_SET_TILES(inVeg, inPfrac, LUC_EXPT )

 IMPLICIT NONE

 INTEGER, INTENT(INOUT) :: inVeg(:,:,:)
 REAL,   INTENT(INOUT) :: inPFrac(:,:,:)
 TYPE (LUC_EXPT_TYPE), INTENT(INOUT) :: LUC_EXPT
 INTEGER :: k, m, n

 

  
   DO k=1,mland
        m = landpt(k)%ilon
        n = landpt(k)%ilat


        if (inVeg(m,n,1).LT.11) THEN ! vegetated

           if (LUC_EXPT%prim_only(k) ) then

              inVeg(m,n,1) = LUC_EXPT%ivegp(k)
              inVeg(m,n,2:3) = 0
              inPFrac(m,n,2:3) = 0
              inPFrac(m,n,1) = 1.0
              if ( LUC_EXPT%grass(k) .gt. 0.01) then
                 IF (LUC_EXPT%mtemp_min20(k).LE. 15.5) THEN
                    inVeg(m,n,2) = 6 ! C3 grass
                 ELSE
                    inVeg(m,n,2) = 7 ! C4 grass
                 ENDIF
                 inPFrac(m,n,1) =  min(LUC_EXPT%primaryf(k),1.0)
                 inPFrac(m,n,2) = 1.0 -  min(LUC_EXPT%primaryf(k),1.0)
              endif
              
           elseif ((.NOT.LUC_EXPT%prim_only(k)) ) then
              inVeg(m,n,1) = LUC_EXPT%ivegp(k)
              inVeg(m,n,2) = LUC_EXPT%ivegp(k)

              if (LUC_EXPT%mtemp_min20(k).LE. 15.5) THEN
                 inVeg(m,n,3) = 6 ! C3 grass
              ELSE
                 inVeg(m,n,3) = 7 ! C4 grass
              ENDIF
              inPFrac(m,n,1) = min(LUC_EXPT%primaryf(k),1.0)
              inPFrac(m,n,2) = min(LUC_EXPT%secdf(k),1.0)
              inPFrac(m,n,3) = 1.0 - inPFrac(m,n,1) - inPFrac(m,n,2) 


           endif
        else
           LUC_EXPT%prim_only(k)=.TRUE.

        endif

! don't consider LUC events in desert or tundra
        if (inveg(m,n,1)==14 .OR.  inveg(m,n,1)==8 ) THEN
          LUC_EXPT%prim_only(k)=.TRUE.
          LUC_EXPT%primaryf(k) = 1.0
          LUC_EXPT%secdf(k) = 0.0
          LUC_EXPT%grass(k) = 0.0
          inPFrac(m,n,1) = 1.0
          inPFrac(m,n,2:3) = 0.0
          inVeg(m,n,2:3) = 0
        endif

     ENDDO
  


991  format(1166(e12.4,2x)) 

END SUBROUTINE LUC_EXPT_SET_TILES
! ==============================================================================

SUBROUTINE READ_ClimateFile(LUC_EXPT)

USE netcdf


  IMPLICIT NONE

  TYPE (LUC_EXPT_type), INTENT(INOUT)       :: LUC_EXPT ! climate variables

  INTEGER*4 :: mp4
  INTEGER*4, parameter   :: pmp4 =0
  INTEGER, parameter   :: fmp4 = kind(pmp4)
  INTEGER*4   :: STATUS
  INTEGER*4   :: FILE_ID, land_ID, nyear_ID, nday_ID, dID, i, land_dim
  CHARACTER :: CYEAR*4, FNAME*99,dum*50

  ! 0 dim arrays
  CHARACTER(len=20),DIMENSION(2) :: A0
  ! 1 dim arrays (npt )
  CHARACTER(len=20),DIMENSION(3) :: A1
 ! 1 dim arrays (integer) (npt )
  CHARACTER(len=20),DIMENSION(2) :: AI1
 
  REAL(r_2), DIMENSION(mland)          :: LAT, LON, TMP
  INTEGER*4 :: TMPI(mland), TMPI0
  LOGICAL            ::  EXISTFILE

  mp4=int(mland,fmp4)
  A0(1) = 'nyears'
  A0(2) = 'year'

  A1(1) = 'latitude'
  A1(2) = 'longitude'
  A1(3) = 'mtemp_min20'
 
 
  AI1(1) = 'iveg'
  AI1(2) = 'biome'
 
  fname = TRIM(LUC_EXPT%ClimateFile)

  INQUIRE( FILE=TRIM( fname ), EXIST=EXISTFILE )

  IF ( .NOT.EXISTFILE) THEN
     write(*,*) fname, ' does not exist!!'
  ELSE
     write(*,*) 'reading biome from : ', fname
  ENDIF
  ! Open NetCDF file:
  STATUS = NF90_OPEN(fname, NF90_NOWRITE, FILE_ID)
  IF (STATUS /= NF90_noerr) CALL handle_err(STATUS)


  ! dimensions:
  ! Land (number of points)

  
  STATUS = NF90_INQ_DIMID(FILE_ID, 'land'   , dID)
  IF (STATUS /= NF90_noerr) CALL handle_err(STATUS)
  STATUS = NF90_INQUIRE_DIMENSION( FILE_ID, dID, LEN=land_dim )
  IF (STATUS /= NF90_noerr) CALL handle_err(STATUS)
  
  IF ( land_dim .NE. mland) THEN
     WRITE(*,*) "Dimension misfit, ", fname
     WRITE(*,*) "land_dim", land_dim
     STOP
  ENDIF

  ! LAT & LON
  STATUS = NF90_INQ_VARID( FILE_ID, A1(1), dID )
  IF (STATUS /= NF90_noerr) CALL handle_err(STATUS)
  STATUS = NF90_GET_VAR( FILE_ID, dID, LAT )
  IF (STATUS /= NF90_noerr) CALL handle_err(STATUS)


  STATUS = NF90_INQ_VARID( FILE_ID, A1(2), dID )
  IF (STATUS /= NF90_noerr) CALL handle_err(STATUS)
  STATUS = NF90_GET_VAR( FILE_ID, dID, LON )
  IF (STATUS /= NF90_noerr) CALL handle_err(STATUS)


! READ 1-dimensional real fields
  DO i = 3, SIZE(A1)
     STATUS = NF90_INQ_VARID( FILE_ID, A1(i), dID )
     IF (STATUS /= NF90_noerr) CALL handle_err(STATUS)
     STATUS = NF90_GET_VAR( FILE_ID, dID, TMP )
     IF (STATUS /= NF90_noerr) CALL handle_err(STATUS)

     SELECT CASE ( TRIM(A1(i)))
     CASE ('mtemp_min20' ) ; LUC_EXPT%mtemp_min20 = TMP
     END SELECT
  END DO

! READ 1-dimensional integer fields
  DO i = 1, SIZE(AI1)

     write(*,*)  TRIM(AI1(i))
     STATUS = NF90_INQ_VARID( FILE_ID, AI1(i), dID )
     IF (STATUS /= NF90_noerr) CALL handle_err(STATUS)
     STATUS = NF90_GET_VAR( FILE_ID, dID, TMPI )
     IF (STATUS /= NF90_noerr) CALL handle_err(STATUS)

     SELECT CASE ( TRIM(AI1(i)))
     CASE ('iveg'      ) ; LUC_EXPT%ivegp     = TMPI
     CASE ('biome'      ) ; LUC_EXPT%biome     = TMPI
     END SELECT
  END DO

! non-woody potential vegetation not considered to undergo LU change
  WHERE (LUC_EXPT%ivegp.GT.5)
     LUC_EXPT%prim_only=.TRUE.
  ENDWHERE

  ! Close NetCDF file:
  STATUS = NF90_close(FILE_ID)
  IF (STATUS /= NF90_noerr) CALL handle_err(STATUS)


END SUBROUTINE READ_CLIMATEFILE

! ==============================================================================
SUBROUTINE READ_LUH2(LUC_EXPT)

IMPLICIT NONE
    
    TYPE (LUC_EXPT_TYPE), INTENT(INOUT) :: LUC_EXPT
    
    REAL    ::  tmp
    REAL, ALLOCATABLE :: tmparr(:,:)
    INTEGER :: t, i, ii, k, x, y, realk
    INTEGER :: fid, vid, tid
    INTEGER :: xds, yds, tds
    INTEGER :: STATUS,  iu
   
 yds = LUC_EXPT%ydimsize 
 xds = LUC_EXPT%xdimsize  
 t = LUC_EXPT%CTSTEP
 IF(.NOT.ALLOCATED(tmparr)) ALLOCATE(tmparr(xds,yds))   

 if (t.LE. LUC_EXPT%nrec) then
    DO i=1, LUC_EXPT%nfile
       IF ( LUC_EXPT%DirectRead ) THEN

          DO k = 1, mland

             STATUS = NF90_GET_VAR( LUC_EXPT%F_ID(i), LUC_EXPT%V_ID(i), tmp, &
                  start=(/land_x(k),land_y(k),t/) )
             CALL HANDLE_ERR(STATUS, "Reading direct from "//LUC_EXPT%TransFile(i) )
             LUC_EXPT%INPUT(i)%VAL(k) = tmp
          END DO
       ELSE

write(6,*) 'land_x=',land_x
write(6,*) 'land_y=',land_y
          STATUS = NF90_GET_VAR(LUC_EXPT%F_ID(i), LUC_EXPT%V_ID(i), tmparr, &
               start=(/1,1,t/),count=(/xds,yds,1/) )
          CALL HANDLE_ERR(STATUS, "Reading from "//LUC_EXPT%TransFile(i) )

          DO k = 1, mland
             LUC_EXPT%INPUT(i)%VAL(k) =tmparr( land_x(k), land_y(k) )
             if  (LUC_EXPT%INPUT(i)%VAL(k).gt.1.0) then
                LUC_EXPT%INPUT(i)%VAL(k) = 0.0
             endif
          END DO

       ENDIF
    ENDDO

 else

    write(*,*) 'warning: past end of LUH2 record'

 endif


 ! Adjust transition areas based on primary wooded fraction 
 WHERE (LUC_EXPT%biome .eq. 3 .or. LUC_EXPT%biome .eq. 11)  ! savanna/ xerophytic woods
    LUC_EXPT%INPUT(ptos)%VAL =  LUC_EXPT%INPUT(ptos)%VAL * 2.0/5.0
    LUC_EXPT%INPUT(ptog)%VAL =  LUC_EXPT%INPUT(ptog)%VAL * 2.0/5.0
    LUC_EXPT%INPUT(gtos)%VAL =  LUC_EXPT%INPUT(gtos)%VAL * 2.0/5.0
    LUC_EXPT%INPUT(stog)%VAL =  LUC_EXPT%INPUT(stog)%VAL * 2.0/5.0
    LUC_EXPT%INPUT(smharv)%VAL =  LUC_EXPT%INPUT(smharv)%VAL * 2.0/5.0
    LUC_EXPT%INPUT(syharv)%VAL =  LUC_EXPT%INPUT(syharv)%VAL * 2.0/5.0
 ELSEWHERE (LUC_EXPT%biome .eq. 12 .or. LUC_EXPT%biome .eq. 13 &
     .or. LUC_EXPT%biome .eq. 15 .or. LUC_EXPT%biome .eq. 16  ) ! shrub
    LUC_EXPT%INPUT(ptos)%VAL =  LUC_EXPT%INPUT(ptos)%VAL * 1.0/5.0
    LUC_EXPT%INPUT(ptog)%VAL =  LUC_EXPT%INPUT(ptog)%VAL * 1.0/5.0
    LUC_EXPT%INPUT(gtos)%VAL =  LUC_EXPT%INPUT(gtos)%VAL * 1.0/5.0
    LUC_EXPT%INPUT(stog)%VAL =  LUC_EXPT%INPUT(stog)%VAL * 1.0/5.0
    LUC_EXPT%INPUT(smharv)%VAL =  LUC_EXPT%INPUT(smharv)%VAL * 1.0/5.0
    LUC_EXPT%INPUT(syharv)%VAL =  LUC_EXPT%INPUT(syharv)%VAL * 1.0/5.0
 ELSEWHERE (LUC_EXPT%biome .eq. 7 .or. LUC_EXPT%biome .eq. 8 &
            .or. LUC_EXPT%biome .eq. 9 .or. LUC_EXPT%biome .eq. 10) ! boreal
    LUC_EXPT%INPUT(ptos)%VAL =  LUC_EXPT%INPUT(ptos)%VAL * 0.8
    LUC_EXPT%INPUT(ptog)%VAL =  LUC_EXPT%INPUT(ptog)%VAL * 0.8
    LUC_EXPT%INPUT(gtos)%VAL =  LUC_EXPT%INPUT(gtos)%VAL * 0.8
    LUC_EXPT%INPUT(stog)%VAL =  LUC_EXPT%INPUT(stog)%VAL * 0.8
    LUC_EXPT%INPUT(smharv)%VAL =  LUC_EXPT%INPUT(smharv)%VAL * 0.8
    LUC_EXPT%INPUT(syharv)%VAL =  LUC_EXPT%INPUT(syharv)%VAL * 0.8
 ELSEWHERE (LUC_EXPT%biome .eq. 5 .or. LUC_EXPT%biome .eq. 6 ) ! DBL
    LUC_EXPT%INPUT(ptos)%VAL =  LUC_EXPT%INPUT(ptos)%VAL * 0.7
    LUC_EXPT%INPUT(ptog)%VAL =  LUC_EXPT%INPUT(ptog)%VAL * 0.7
    LUC_EXPT%INPUT(gtos)%VAL =  LUC_EXPT%INPUT(gtos)%VAL * 0.7
    LUC_EXPT%INPUT(stog)%VAL =  LUC_EXPT%INPUT(stog)%VAL * 0.7
    LUC_EXPT%INPUT(smharv)%VAL =  LUC_EXPT%INPUT(smharv)%VAL * 0.7
    LUC_EXPT%INPUT(syharv)%VAL =  LUC_EXPT%INPUT(syharv)%VAL * 0.7
 ENDWHERE

END SUBROUTINE READ_LUH2
! ==============================================================================


END MODULE CABLE_LUC_EXPT
