! *****************************COPYRIGHT*******************************
! (C) Crown copyright Met Office. All rights reserved.
! For further details please refer to the file COPYRIGHT.txt
! which you should have received as part of this distribution.
! *****************************COPYRIGHT*******************************
!
! Routine to calculate the gridbox mean land surface parameters from
! the areal fractions of the surface types and the structural
! properties of the plant functional types.
!
! This routine no longer calculates the max infiltration rate. This
! is now done in science/soil/infiltration_rate.F90 and called seperately
! as required.
!
! Future developments make calling infiltration_rate from within sparm
! undesirable (eg smcl-dependence)
!
! *********************************************************************
MODULE sparm_mod
CHARACTER(LEN=*), PARAMETER, PRIVATE :: ModuleName='SPARM_MOD'

CONTAINS

SUBROUTINE sparm (land_pts, nsurft, surft_pts, surft_index,                    &
                  frac_surft, canht_pft, lai_pft, z0m_soil_gb,                 &
                  catch_snow_surft, catch_surft, z0_surft, z0h_bare_surft,     &
                  ztm_gb)

!Use in relevant subroutines
USE pft_sparm_mod,            ONLY: pft_sparm
USE nvg_sparm_mod,            ONLY: nvg_sparm

!Use in relevant variables
USE jules_surface_types_mod,  ONLY: lake, npft, ntype
USE jules_vegetation_mod,     ONLY: can_model
USE blend_h,                  ONLY: lb
USE jules_surface_mod,        ONLY: i_aggregate_opt, l_aggregate
USE jules_snow_mod,           ONLY: cansnowtile, snowloadlai
USE c_z0h_z0m,                ONLY: z0h_z0m

USE stochastic_physics_run_mod, ONLY: l_rp2, i_rp_scheme, i_rp2b, z0hm_pft_rp

USE parkind1,                 ONLY: jprb, jpim
USE yomhook,                  ONLY: lhook, dr_hook

USE um_types, ONLY: real_jlslsm

IMPLICIT NONE

INTEGER, INTENT(IN) ::                                                         &
  land_pts,                                                                    &
    ! Number of land points to be processed.
  nsurft,                                                                      &
    ! Number of surface tiles.
  surft_pts(ntype),                                                            &
    ! Number of land points which include the nth surface type.
  surft_index(land_pts,ntype)
    ! Indices of land points which include the nth surface type.

REAL(KIND=real_jlslsm), INTENT(IN) ::                                          &
  frac_surft(land_pts,ntype),                                                  &
    ! Fractional cover of each surface type.
  canht_pft(land_pts,npft),                                                    &
    ! Vegetation height (m).
  lai_pft(land_pts,npft),                                                      &
    ! Leaf area index.
  z0m_soil_gb(land_pts),                                                       &
    ! z0m of bare soil (m)
  ztm_gb(land_pts)
    ! Roughness length

REAL(KIND=real_jlslsm), INTENT(OUT) ::                                         &
  catch_snow_surft(land_pts,nsurft),                                           &
    ! Snow capacity for tile (kg/m2)
  catch_surft(land_pts,nsurft),                                                &
    ! Canopy capacity for each tile (kg/m2).
  z0_surft(land_pts,nsurft),                                                   &
    ! Roughness length for each tile (m).
  z0h_bare_surft(land_pts,nsurft)
    ! Snow-free thermal roughness length for each tile (m).


!-----------------------------------------------------------------------------
! Local variables.
!-----------------------------------------------------------------------------
INTEGER ::                                                                     &
  j,l,n  ! Loop counters

REAL(KIND=real_jlslsm) ::                                                      &
  catch(land_pts),                                                             &
    ! GBM canopy capacity (kg/m2).
  catch_t(land_pts,ntype),                                                     &
    ! Canopy capacities for types not tiles (kg/m2); required for l_aggregate.
  fz0(land_pts),                                                               &
    ! Aggregation function of Z0.
  fz0h(land_pts),                                                              &
    ! Aggregation function of Z0H.
  z0(land_pts),                                                                &
    ! GBM roughness length (m).
  z0h(land_pts),                                                               &
    ! GBM thermal roughness length (m).
  z0_t(land_pts,ntype)
    ! Roughness lengths for types not tiles (m); required for l_aggregate.

INTEGER(KIND=jpim), PARAMETER :: zhook_in  = 0
INTEGER(KIND=jpim), PARAMETER :: zhook_out = 1
REAL(KIND=jprb)               :: zhook_handle

CHARACTER(LEN=*), PARAMETER :: RoutineName='SPARM'

!-----------------------------------------------------------------------------
!end of header
!-----------------------------------------------------------------------------
IF (lhook) CALL dr_hook(ModuleName//':'//RoutineName,zhook_in,zhook_handle)

!-----------------------------------------------------------------------------
! Set parameters for vegetated surface types
!-----------------------------------------------------------------------------
IF ( l_rp2 .AND. i_rp_scheme == i_rp2b) THEN
  DO n = 1,npft
    z0h_z0m(n) = z0hm_pft_rp(n)
  END DO
END IF

IF ( .NOT. l_aggregate ) THEN
  !---------------------------------------------------------------------------
  ! Set parameters for vegetated surface types
  !---------------------------------------------------------------------------
  DO n = 1,npft
    CALL pft_sparm (land_pts, n, surft_pts(n), surft_index(:,n),               &
                    canht_pft(:,n), lai_pft(:,n), catch_surft(:,n),            &
                    z0_surft(:,n))
  END DO

  ! cansnowtile is only used when l_aggregate is .FALSE. - needs to be
  ! consistent with logic where cansnowtile is set.
  IF (can_model  ==  4 ) THEN
    DO n = 1,npft
      IF ( cansnowtile(n) ) THEN
        DO j = 1,surft_pts(n)
          l = surft_index(j,n)
          catch_snow_surft(l,n) = snowloadlai * lai_pft(l,n)
        END DO
      END IF
    END DO
  END IF

  !-----------------------------------------------------------------------------
  ! Set parameters for non-vegetated surface types
  !-----------------------------------------------------------------------------
  ! It would be nice not to have to call this every time, however, catch_surft,
  ! z0_surft gets set to zero somewhere so needs to be called.
  CALL nvg_sparm (land_pts, surft_pts, surft_index, z0m_soil_gb, ztm_gb,       &
                  catch_surft, z0_surft)

ELSE ! l_aggregate
  !---------------------------------------------------------------------------
  ! Set parameters for vegetated surface types
  !---------------------------------------------------------------------------
  DO n = 1,npft
    CALL pft_sparm (land_pts, n, surft_pts(n), surft_index(:,n),               &
                    canht_pft(:,n), lai_pft(:,n), catch_t(:,n), z0_t(:,n))
  END DO

  !-----------------------------------------------------------------------------
  ! Set parameters for non-vegetated surface types
  !-----------------------------------------------------------------------------
  CALL nvg_sparm (land_pts, surft_pts, surft_index, z0m_soil_gb, ztm_gb,       &
                  catch_t, z0_t)

  !---------------------------------------------------------------------------
  ! Form means and copy to tile arrays if required for aggregate tiles
  !---------------------------------------------------------------------------
  DO l = 1,land_pts
    catch(l)  = 0.0
    fz0(l)    = 0.0
    fz0h(l)   = 0.0
    z0(l)     = 0.0
  END DO

  DO n = 1,ntype
    DO j = 1,surft_pts(n)
      l = surft_index(j,n)
      fz0(l) = fz0(l) + frac_surft(l,n) / (LOG(lb / z0_t(l,n)))**2
      ! Explicit aggregation of z0h if required.
      IF (i_aggregate_opt == 1) THEN
        fz0h(l) = fz0h(l) + frac_surft(l,n) /                                  &
           ( LOG(lb / z0_t(l,n)) * LOG(lb / (z0h_z0m(n) * z0_t(l,n))) )
      END IF
    END DO
  END DO

  DO l = 1,land_pts
    z0(l) = lb * EXP(-SQRT(1.0 / fz0(l)))
    ! Explicit aggregation of z0h if required.
    IF (i_aggregate_opt == 1) THEN
      z0h(l) = lb * EXP(-1.0 / (fz0h(l) * LOG(lb / z0(l))) )
    END IF
  END DO

  DO n = 1,ntype
    DO j = 1,surft_pts(n)
      l = surft_index(j,n)
      catch(l) = catch(l) + frac_surft(l,n) * catch_t(l,n)
    END DO
  END DO

  DO l = 1,land_pts
    IF ( lake > 0 ) THEN
      ! Canopy capacity is average over non-lake surface types
      IF ( frac_surft(l,lake) < 1.0 ) THEN
        catch_surft(l,1) = catch(l) / (1.0 - frac_surft(l,lake))
      ELSE
        catch_surft(l,1) = 0.0
      END IF
    ELSE
      catch_surft(l,1) = catch(l)
    END IF
    z0_surft(l,1) = z0(l)
    IF (i_aggregate_opt == 1) z0h_bare_surft(l,1) = z0h(l)
  END DO
END IF  !  l_aggregate

IF (lhook) CALL dr_hook(ModuleName//':'//RoutineName,zhook_out,zhook_handle)
RETURN
END SUBROUTINE sparm
END MODULE sparm_mod
